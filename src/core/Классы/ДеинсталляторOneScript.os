Перем Лог;

// Удалить установленную версию OneScript
//
// Параметры:
//   УдаляемаяВерсия - Строка - Алиас версии, которую необходимо удалить
//
Процедура УдалитьOneScript(Знач УдаляемаяВерсия) Экспорт
	
	Если НЕ ВерсииOneScript.ВерсияУстановлена(УдаляемаяВерсия) Тогда
		Лог.Информация("Не обнаружена требуемая версия <%1>", УдаляемаяВерсия);
		Возврат;
	КонецЕсли;

	КаталогУстановки = ПараметрыOVM.КаталогУстановкиПоУмолчанию();
	КаталогУстановкиВерсии = ОбъединитьПути(КаталогУстановки, УдаляемаяВерсия);

	Лог.Отладка("Удаляю файлы %1", КаталогУстановкиВерсии);

	УдалитьФайлы(КаталогУстановкиВерсии);

	Лог.Информация("Версия %1 удалена", УдаляемаяВерсия);

КонецПроцедуры

// Удалить все установленные версии OneScript
//
Процедура УдалитьВсеВерсииOneScript() Экспорт
	СписокУстановленныхВерсий = ВерсииOneScript.ПолучитьСписокУстановленныхВерсий();

	Для Каждого ВерсияКУдалению Из СписокУстановленныхВерсий Цикл
		Если ВерсияКУдалению.ЭтоСимлинк Тогда
			Продолжить;
		КонецЕсли;
		УдалитьOneScript(ВерсияКУдалению.Алиас);
	КонецЦикла;

КонецПроцедуры

Лог = ПараметрыOVM.ПолучитьЛог();
